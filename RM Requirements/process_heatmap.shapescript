shape main
{
	layouttype="border";
	noshadow=true;
	DefSize(250,40);
	//set the fill color based on tagged value "Heat Map"
	if(hastag("Heat Map","good"))
	{
		//light green
		SetFillColor(112,255,145);
	}
	else if(hastag("Heat Map","problematic"))
	{
		//light yellow
		SetFillColor(255,251,71);
	}
	else if(hastag("Heat Map","poor"))
	{
		//light red
		SetFillColor(255,142,142);
	}
	else if(hastag("Heat Map","not evaluated"))
	{
		//white
		SetFillColor(255,255,255);
	}
	else if(hastag("Heat Map","missing"))
	{
		//light purple
		SetFillColor(227,168,255);
	}
	else
	{
		//default white
		setfillcolor(255,255,255);
	}

	//main shape
	setFontColor(0,0,0);

	startPath();
	setfixedregion(80,0,100,0);
	moveto(0,0);
	lineto(80,0);
	lineto(100,50);
	lineto(80,100);
	setfixedregion(0,0,20,0);
	lineto(0,100);
	lineto(20,50);
	lineto(0,0);
	endPath();
	fillandstrokePath();

	//addsubshape("topPadding","n");
	addsubshape("leftPadding","w");
	addsubshape("rightPadding","e");
	addsubshape("name","center");
	shape name
	{
		v_align="center";
		h_align="center";
		printwrapped("#name#");
	}
	shape topPadding
	{
		preferredheight=10;
	}
	shape leftPadding
	{
		preferredWidth = 20;
	}
	shape rightPadding
	{
		preferredWidth = 10;
	}
}